#!/usr/bin/perl -w
use strict;

my %word_count;
my %source_word_count;
my %target_word_count;
my %rule_count;
my %source_count;
my %target_count;

while (<>) {
  chomp;
  my ($lhs,$src,$tgt,$alignment,undef) = split /\t/, $_, 5;
  $rule_count{$_}++;
  $source_count{$src}++;
  $target_count{$tgt}++;

  my @ss = terminals($src);
  my @ts = terminals($tgt);
  foreach (word_pairs(\@ss, \@ts, $alignment)) {
    my ($s, $t) = split /\t/;
    $word_count{$_}++;
    $source_word_count{$s}++;
    $target_word_count{$t}++;
  }
}

sub terminals {
  my $rhs = shift;
  grep { !/,\d+\]$/ } split " ", $rhs;
}

sub word_pairs {
  my ($from, $to, $alignment) = @_;
  my %seen;
  my @result;
  while ($alignment =~ /(\d+)-(\d+)/g) {
    push @result, "$$from[$1]\t$$to[$2]";
    $seen{"$1-"} = undef;
    $seen{"-$2"} = undef;
  }
  for (my $i = 0; $i < @$from; $i++) {
    push @result, "$$from[$i]\t" unless exists $seen{"$i-"};
  }
  for (my $i = 0; $i < @$to; $i++) {
    push @result, "\t$$to[$i]" unless exists $seen{"-$i"};
  }
  return @result;
}

sub word_lexprobs {
  my ($s, $t) = @_;
  my $joint = $word_count{"$s\t$t"} // 1;
  my $sc = $source_word_count{$s} // 1;
  my $tc = $target_word_count{$t} // 1;
  return ( $joint / $tc, $joint / $sc);
}

sub lexprob {
  my ($src, $tgt, $alignment) = @_;
  my @ss = terminals $src;
  my @ts = terminals $tgt;
  my %terms;
  while ($alignment =~ /(\d+)-(\d+)/g) {
    my ($ps, $pt) = word_lexprobs($ss[$1], $ts[$2]);
    $terms{"$1-"} += $pt;
    $terms{"-$2"} += $pt;
    $terms{"count$1-"}++;
    $terms{"count-$2"}++;
  }
  my $s = 1;
  my $t = 1;
  foreach (keys %terms) {
    next if /^c/;  # count
    my $avg = $terms{$_} / $terms{"count$_"};
    if (/-$/) {
      $s *= $avg;
    } else {
      $t *= $avg;
    }
  }
  for (my $i = 0; $i < @ss; $i++) {
    unless (exists $terms{"$i-"}) {
      my ($unaligned, undef) = word_lexprobs($ss[$i], "");
      $s *= $unaligned;
    }
  }
  for (my $i = 0; $i < @ts; $i++) {
    unless (exists $terms{"$i-"}) {
      my (undef, $unaligned) = word_lexprobs("", $ts[$i]);
      $t *= $unaligned;
    }
  }
  return (log($s), log($t));
}

foreach (keys %rule_count) {
  next unless $rule_count{$_} > 1;
  print "$_\t$rule_count{$_}\t";
  my (undef,$src,$tgt,$alignment,undef) = split /\t/, $_, 5;
  my $t_given_s = log($rule_count{$_} / $source_count{$src});
  my $s_given_t = log($rule_count{$_} / $target_count{$tgt});
  print "p(e|f)=$t_given_s p(f|e)=$s_given_t";
  ($s_given_t, $t_given_s) = lexprob($src, $tgt, $alignment);
  print " lex(e|f)=$t_given_s lex(f|e)=$s_given_t";
  print "\n";
}
