#!/usr/bin/perl -w
use strict;

# input: source \t target \t alignment
# output: s \t t for each aligned word pair
#         s \t "" for unaligned source words
#         "" \t t for unaligned target words

my %counts;

while (<>) {
  chomp;
  my ($src,$tgt,$alignment) = split /\t/;
  my @stok = split " ", $src;
  my @ttok = split " ", $tgt;
  for (my $i = 0; $i < @stok; $i++) {
    $counts{"$stok[$i]\t"}++ unless ($alignment =~ /(?:^|\s+)$i-/);
  }
  for (my $i = 0; $i < @ttok; $i++) {
    $counts{"\t$ttok[$i]"}++ unless ($alignment =~ /-$i(?:$|\s+)/);
  }
  while ($alignment =~ /(\d+)-(\d+)/g) {
    $counts{"$stok[$1]\t$ttok[$2]"}++ if ($1 < @stok and $2 < @ttok);
  }
}

print "$_\t$counts{$_}\n" foreach (keys %counts);
